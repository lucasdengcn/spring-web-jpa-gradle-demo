import java.text.SimpleDateFormat

tasks.named('jar') {
    archiveClassifier.set('')
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                "Build-Jdk"             : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                "Build-OS"              : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        library(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}

artifactory {
    publish {
        // Define the Artifactory URL for publishing artifacts
        contextUrl = 'http://127.0.0.1:8081/artifactory'
        // Define the project repository to which the artifacts will be published
        repository {
            releaseRepoKey = "local-release" // The Artifactory repository key to publish to
            snapshotRepoKey = "$System.env.ARTIFACTORY_LOCAL_REPO" // The Artifactory repository key to publish to
            username = "$System.env.PLATFORM_USERNAME" // The publisher user name
            password = "$System.env.PLATFORM_ADMIN_TOKEN" // The publisher password
        }

        // Include all configured publications for all the modules
        defaults {
            publications('ALL_PUBLICATIONS')
        }
    }
}